import com.google.gms.googleservices.GoogleServicesPlugin

apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    signingConfigs {
        signing {
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }

    compileSdkVersion 28

    def getVersionName = { ->
        def version = new ByteArrayOutputStream()

        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = version
        }
        version = version.toString().split('-')
        return "${version[0]}.${version[1]}"
    }
    def getVersionCode = { ->
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = code
        }
        return Integer.valueOf(code.toString().trim())
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = applicationId + "-${variant.versionName}.apk"
        }
    }

    defaultConfig {
        applicationId "com.example.androidlabs"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName getVersionName()
        signingConfig signingConfigs.signing
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }

    flavorDimensions "version"

    productFlavors {
        dev {
            dimension "version"

            versionNameSuffix "-dev"
        }
        primary {

            dimension "version"
        }
        /*
        free {

            dimension "paid"
        }

        premium {

            dimension "paid"
        }*/

    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0-alpha1'
    implementation 'com.google.android.material:material:1.0.0-alpha1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation 'android.arch.navigation:navigation-fragment:1.0.0-alpha07'
    implementation "android.arch.navigation:navigation-ui:1.0.0-alpha07"
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.0.0"
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation "androidx.room:room-runtime:2.1.0-alpha02"
    annotationProcessor "androidx.room:room-compiler:2.1.0-alpha02"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.firebase:firebase-auth:16.0.5'
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.google.firebase:firebase-database:16.0.4'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'


}

apply plugin: 'com.google.gms.google-services'
GoogleServicesPlugin.config.disableVersionCheck = true
