apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    signingConfigs {
        signing {
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }

    compileSdkVersion 28

    def getVersionName = { ->
        def major = "0"
        def date = new ByteArrayOutputStream()
        def patch = new ByteArrayOutputStream()
        def minor = new ByteArrayOutputStream()

        exec {
            commandLine 'git', 'rev-list', '--count', 'master'
            standardOutput = minor
        }
        minor = (Integer.valueOf(minor.toString().trim()) - 1).toString()

        exec {
            commandLine 'git', 'log', '--pretty=format:\"%cd\"', '-n', '1', 'master'
            standardOutput = date
        }
        exec {
            commandLine 'git', 'rev-list', '--count', 'develop', '--since', date.toString().trim()
            standardOutput = patch
        }
        patch = (Integer.valueOf(patch.toString().trim()) - 1).toString()

        return "${major}.${minor}.${patch}"
    }
    def getVersionCode = { ->
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = code
        }
        return Integer.valueOf(code.toString().trim())
    }



    defaultConfig {

        applicationId "com.example.androidlabs"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName getVersionName()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.signing
        }
    }

    flavorDimensions "version"
    productFlavors {
        dev {
            dimension "version"
            versionCode getVersionCode()
            versionName getVersionName()

            versionNameSuffix "-dev"
            signingConfig signingConfigs.signing


        }
        primary {
            dimension "version"
            signingConfig signingConfigs.signing

            versionCode getVersionCode()
            versionName getVersionName()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0-rc02'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
